import kr.entree.spigradle.data.Load

plugins {
    id 'java'
    id 'kr.entree.spigradle' version '2.1.1'
}

group 'kr.entree'
version '1.0-SNAPSHOT'

compileJava.options.encoding 'UTF-8'

repositories {
    protocolLib()
    jitpack() // For vault
}

dependencies {
    compileOnly spigot()
    compileOnly protocolLib()
    compileOnly(vaultAll()) { // instead of vault() for the dependency resolution by debug task 'prepareSpigotPlugins'.
        transitive = false // No want to import vault's internal dependencies.
    }
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation mockBukkit()
}

spigot {
    description 'A sample plugin'
    depends 'ProtocolLib', 'Vault'
    load Load.STARTUP // or implicitly "load STARTUP".
    commands {
        give {
            aliases 'giv', 'i'
            description 'A give command.'
            permission 'sample.give'
            permissionMessage 'You do not have the permission.'
        }
    }
    permissions {
        'sample.give' {
            description 'Allows give command'
            defaults 'true'
        }
        'sample.*' {
            description 'Wildcard permission'
            defaults 'op'
            children = ['test.foo': true]
        }
    }
}
